#Display full view of article
prototype(Sfi.News:NewsDetail) < prototype(TYPO3.Neos:Content) {
	templatePath = ${'resource://Sfi.News/Private/Templates/NewsDetail' + q(node).parent().property('variant') + '.html'}
	@override.configuration = ${Configuration.setting('Sfi.News.newsDetail')}
	configuration = ${configuration}

	title = ${q(node).property('title')}
	teaser = ${q(node).property('teaser')}
	author = ${q(node).property('author')}
	date = ${q(node).property('date')}

	coverPhoto = TYPO3.Neos.NodeTypes:Image{
		@override.node = ${q(node).children('coverPhoto').get(0)}
	}
	gallery = Sfi.Widgets:Carousel{
		content.collection = ${q(node).children('gallery').children()}
		attributes.class = 'Carousel js-carousel-2x'
	}

	main = TYPO3.Neos:ContentCollection
	main.nodePath = 'main'	

	category = ${q(node).parent().get(0)}

	#####
	moreNews = Sfi.News:NewsList{
		attributes.class = 'NewsList'
	}
}

#Breif rendering of article, used in NewsList
prototype(Sfi.News:NewsShort) < prototype(TYPO3.Neos:Content) {
	templatePath = 'resource://Sfi.News/Private/Templates/NewsShort.html'
	@override.configuration = ${Configuration.setting('Sfi.News.newsShort')}
	configuration = ${configuration}

	title = ${q(node).property('title')}
	date = ${q(node).property('date')}
	teaser = ${q(node).property('teaser')}
	# first image found becomes the thumbnail image 
	thumbnailImage = ${q(node).children('coverPhoto').get(0)}
	#parrent node is the category
	category = ${q(node).parent().get(0)}
}

prototype(Sfi.News:NewsList) < prototype(TYPO3.Neos:Content) {
	templatePath = 'resource://Sfi.News/Private/Templates/List.html'
	@override.configuration = ${Configuration.setting('Sfi.News.newsList')}
	configuration = ${configuration}

	#Public
	newsCollection = ${q(site).find('[instanceof Sfi.News:News]').get()}
	#sort
	newsCollection.@process.sort = ${q(value).count() > 0 ? q(value).sort(configuration.sortProperty, configuration.sortOrder).get() : value}
	#limit
	newsCollection.@process.slice = ${q(value).count() > 0 ? q(value).slice(0, String.toInteger(configuration.numberOfItems)).get() : value}
	#This object will be used to render news articles
	listTitle = ${configuration.listTitle}
	attributes.class = ${configuration.class}

	#Public
	newsItem = Sfi.News:NewsShort
}


prototype(Sfi.News:TopShort) < prototype(Sfi.News:NewsShort) {
	templatePath = 'resource://Sfi.News/Private/Templates/TopShort.html'
	@override.configuration = ${Configuration.setting('Sfi.News.topShort')}
}

prototype(Sfi.News:TopList) < prototype(Sfi.News:NewsList) {
	templatePath = 'resource://Sfi.News/Private/Templates/List.html'
	@override.configuration = ${Configuration.setting('Sfi.News.topList')}

	newsCollection = ${q(site).find('[instanceof Sfi.News:AbstractNews]').filter('[important = TRUE]').get()}
	newsItem>
	newsItem = Sfi.News:TopShort
}