prototype(Sfi.News:AnnouncementDetail) < prototype(Sfi.News:NewsDetail) {
	templatePath = 'resource://Sfi.News/Private/Templates/AnnouncementDetail.html'
	@override.configuration = ${Configuration.setting('Sfi.News.announcementDetail')}
	configuration = ${configuration}

	place = ${q(node).property('place')}
	dateStart = ${q(node).property('dateStart')}

	moreItems = Sfi.News:NewsRelated {
		prototype(Sfi.News:Listable) {
			collection = ${q(site).children('announcements').find('[instanceof Sfi.News:Announcement]').get()}
			collection.@process.filterByDate = ${q(value).count() > 0 ? q(value).filterByDate('date', Date.Now()).get() : value}
		}
	}
}

prototype(Sfi.News:AnnouncementShort) < prototype(Sfi.News:NewsShort) {
	templatePath = 'resource://Sfi.News/Private/Templates/AnnouncementShort.html'
	@override.configuration = ${Configuration.setting('Sfi.News.announcementShort')}
	configuration = ${configuration}

	place = ${String.stripTags(q(node).property('place'))}
	dateStart = ${q(node).property('dateStart')}
}

prototype(Sfi.News:AnnouncementsList) < prototype(Sfi.News:AbstractList) {
	@override.configuration = ${Configuration.setting('Sfi.News.announcementsList')}
	configuration = ${configuration}

	# Get announcements collection here and push it to context vars, to be used both in Listable and in @cache
	collection = ${q(site).children('announcements').find('[instanceof Sfi.News:Announcement]').get()}
	collection.@process.filterByDate = ${q(value).count() > 0 ? q(value).filterByDate('date', Date.Now()).get() : value}
	collection.@process.sortAgain = ${this.sortProperty ? q(value).sort(this.sortProperty, 'ASC').get() : value}
	@override.collection = ${this.collection}
	prototype(Sfi.News:Listable) {
		collection = ${collection}
		itemRenderer = Sfi.News:AnnouncementShort
	}
	@cache {
		mode = 'cached'
		# First element in collection is the one, that expires soonest (see sortAgain)
		maximumLifetime = ${q(collection).get(0).properties.date.timestamp - Date.now().timestamp}
		entryIdentifier {
			node = ${node}
			editPreviewMode = ${editPreviewMode}
		}
		entryTags {
			1 = ${'NodeType_Sfi.News:Announcement'}
		}
	}
}
